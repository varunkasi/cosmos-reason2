default:
  just --list

default_cuda_name := "cu128"

# Install the repository
install cuda_name=default_cuda_name *args:
  echo {{cuda_name}} > .cuda-name
  uv sync --extra={{cuda_name}} {{args}}

# Run uv sync
_uv-sync *args:
  if [ ! -f .cuda-name ]; then \
    echo {{default_cuda_name}} > .cuda-name; \
  fi
  uv sync --extra=$(cat .cuda-name) {{args}}

# Run a command in the package environment
run *args: _uv-sync
  uv run --no-sync {{args}}

# Setup the repository
_pre-commit-install:
  uv python install
  uv tool install "pre-commit>=4.5.0"
  pre-commit install -c .pre-commit-config-base.yaml

_pre-commit-base *args:
  pre-commit run -c .pre-commit-config-base.yaml -a {{args}}

_pre-commit *args:
  pre-commit run -a {{args}} || pre-commit run -a {{args}}

# Run linting and formatting
lint: _pre-commit-install _pre-commit-base _pre-commit notebooks-sync

# Run tests
test: _uv-sync
  uv run --no-sync pytest -vv

# Run pip-licenses
_pip-licenses *args:
  #!/usr/bin/env bash
  set -euxo pipefail
  venv_dir="$(uv cache dir)/tmp"
  mkdir -p "${venv_dir}"
  venv_dir=$(mktemp -d -p "${venv_dir}")
  uv venv --clear "$venv_dir"
  python_path="$venv_dir/bin/python"
  uv pip install --no-deps -r ci/license-requirements.txt --python $python_path
  uvx pip-licenses@5.5.0 \
    --python $python_path \
    --format=plain-vertical \
    --with-license-file \
    --no-license-path \
    --no-version \
    --with-urls \
    --output-file ATTRIBUTIONS.md \
    {{args}}
  rm -rf "$venv_dir"

# Update the license
license: _pip-licenses

# Sync jupytext notebooks
[working-directory: 'examples/notebooks']
notebooks-sync:
  uvx --with "ruff==0.14.8" jupytext@1.18.1 --sync *.ipynb --pipe 'ruff format -'

# Run the docker container
_docker build_args='' run_args='':
  #!/usr/bin/env bash
  set -euxo pipefail
  docker build {{build_args}} .
  image_tag=$(docker build {{build_args}} -q .)
  docker run \
    -it \
    --gpus all \
    --ipc=host \
    --rm \
    -v .:/workspace \
    -v /workspace/.venv \
    -v /workspace/examples/cosmos_rl/.venv \
    -v /root/.cache:/root/.cache \
    -e HF_TOKEN="$HF_TOKEN" \
    {{run_args}} \
    $image_tag

# Deploy using docker compose (Dockerfile auto-selects CUDA by architecture)
deploy *args:
  #!/usr/bin/env bash
  set -euo pipefail
  if [ -f "$HOME/.env_keys" ]; then
    source "$HOME/.env_keys"
  fi
  if [ -z "${HF_TOKEN:-}" ]; then
    echo "ERROR: HF_TOKEN is not set. Add 'export HF_TOKEN=hf_...' to ~/.env_keys"
    exit 1
  fi
  if [ "$(uname -m)" = "aarch64" ]; then
    echo "Detected architecture: $(uname -m), default CUDA 13.0.0"
  else
    echo "Detected architecture: $(uname -m), default CUDA 12.8.1"
  fi
  docker compose up --build vllm web {{args}}

# Run the CUDA 12.8 docker container.
docker-cu128 *run_args: (_docker '--build-arg=CUDA_VERSION_AMD64=12.8.1 --build-arg=CUDA_VERSION_ARM64=12.8.1' run_args)

# Run the CUDA 13.0 docker container.
docker-cu130 *run_args: (_docker '--build-arg=CUDA_VERSION_AMD64=13.0.0 --build-arg=CUDA_VERSION_ARM64=13.0.0' run_args)

# Run the nightly docker container.
docker-nightly *run_args: (_docker '-f docker/nightly.Dockerfile' run_args)
